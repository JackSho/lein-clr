(ns leiningen.clr
  (:require [clojure.pprint  :as pp]
            [clojure.string  :as str]
            [clojure.java.io :as io]
            [leiningen.clr.internal   :as in]
            [leiningen.core.classpath :as lc])
  (:import (java.io Reader BufferedReader File InputStream InputStreamReader
                    OutputStream Writer)
           (java.util Map)))


;; ===== Project keys =====

(def pk-target-path    [:clr :target-path])
(def pk-cmd-templates  [:clr :cmd-templates])
(def pk-compile-cmd    [:clr :compile-cmd])
(def pk-main-cmd       [:clr :main-cmd])
(def pk-assembly-paths [:clr :assembly-paths])
(def pk-deps-cmds      [:clr :deps-cmds])
(def pk-deps-regex     [:clr :deps-regex])
(def pk-load-paths     [:clr :load-paths])

(def pk-aot [:clr :aot])


;; ===== Target paths =====

(defn target-path
  [project]
  (or (get-in project pk-target-path)
      (str (:target-path project) File/separator "clr")))


(defn target-bin-path
  [project]
  (str (target-path project) File/separator "bin"))


(defn target-lib-path
  [project]
  (str (target-path project) File/separator "lib"))


;; ===== Project keys =====

(defn cmd-templates
  [project]
  (get-in project pk-cmd-templates))


(defn proj-key-cmd
  "Return a sequence of command and arguments"
  [proj-key default-value project]
  (or (when-let [cmd (get-in project proj-key)]
        (assert (vector? cmd))
        (in/resolve-path cmd (cmd-templates project)))
      default-value))


(defn default-cmd
  [^String cmd] {:pre [(string? cmd)]}
  (let [^String os-name (System/getProperty "os.name")]
    (if (and os-name (.startsWith ^String os-name "Windows"))
      [cmd]
      ["mono" (or (in/which cmd) cmd)])))


(def clj-compile-cmd (partial proj-key-cmd pk-compile-cmd
                              (default-cmd "Clojure.Compile.exe")))


(def clj-main-cmd (partial proj-key-cmd pk-main-cmd
                           (default-cmd "Clojure.Main.exe")))


(defn all-load-paths
  [project]
  (let [load-paths (get-in project pk-load-paths)]
    (when load-paths
      (in/warn "[:clr :load-paths] is deprecated and will be discontinued soon. Consider using :resource-paths"))
    (->> load-paths
         (map #(in/resolve-path % (cmd-templates project)))
         (concat (lc/get-classpath project)))))


(defn aot-namespaces
  [project]
  (let [aot-nses (or (get-in project pk-aot)
                     (:aot project))
        all-nses (mapcat in/scan-namespaces (all-load-paths project))]
    (mapcat (fn [each]
              (if (instance? java.util.regex.Pattern each)
                (filter (partial re-matches each) all-nses)
                [(str each)]))
      aot-nses)))


(defn fetch-deps
  "Fetch dependencies by running commands. Do not fetch if already fetched earlier."
  [project]
  (let [lib  (target-lib-path project)
        cmds (get-in project pk-deps-cmds)]
    (if (and (not (.exists (File. lib)))
             (seq cmds))
      (do
        (in/verbose "Making sure" lib "exists")
        (in/mkdir-p lib)
        (in/verbose "Fetching dependencies" cmds)
        (->> cmds
             (map #(in/resolve-path % (cmd-templates project)))
             (map #(in/run-cmd % lib))
             dorun))
      (in/verbose "Not fetching dependencies. Run `lein clean` to re-fetch."))))


(defn assembly-search-paths
  [project]
  (->> (get-in project pk-assembly-paths)
       (map in/as-vector)
       (concat [[(target-lib-path project) (get-in project pk-deps-regex)]])
       (mapcat in/filter-assembly-paths)))


(defn asm-load-init
  [project init-file]
  (fetch-deps project)
  (in/spit-assembly-load-instruction
    init-file
    (assembly-search-paths project))
  init-file)


;;; ========== Tasks ==========


(defn task-clean
  [project]
  (let [tp (target-path project)]
    (in/verbose "Recursively deleting directory: " tp)
    (in/rm-rf tp)))


(defn task-compile
  [project namespaces]
  (let [allp (all-load-paths project)
        srcp (concat (:source-paths project) (:test-paths project))
        nses (cond
               (empty? namespaces)             (aot-namespaces project)
               (and (= 1 (count namespaces))
                (= ":all" (first namespaces))) (mapcat in/scan-namespaces srcp)
               :otherwise                      namespaces)
        exec (concat (clj-compile-cmd project) nses)]
    (in/with-process-builder pb (:root project) exec
      (in/configure-load-path (.environment ^ProcessBuilder pb) allp)
      (in/configure-compile-path (.environment ^ProcessBuilder pb)
                                 (target-bin-path project))
      (apply in/verbose "Running: " (map pr-str exec))
      (in/run-process pb))))


(defn task-help
  []
  (println "
Available tasks:
clean    delete files generated by lein-clr
compile  compile Clojure source into .dll, .exe and .pdb/.mdb files
help     show this help screen
repl     load a REPL with sources into CLOJURE_LOAD_PATH
run      run a namespace having `-main` function
test     run tests in specified/all test namespaces
"))


(defn task-repl
  [project]
  (let [init-file  (asm-load-init project (in/get-temp-file))
        allp (all-load-paths project)
        _    (in/verbose-init-with init-file)
        exec (concat (clj-main-cmd project) ["-i" init-file "-r"])]
    (in/with-process-builder pb (:root project) exec
      (in/configure-load-path (.environment ^ProcessBuilder pb) allp)
      (apply in/verbose "Running: " (map pr-str exec))
      (in/run-process pb :pipe-input))))


(def no-main "No :main namespace specified in project.clj.")
(def no-ns   "Option -m requires a namespace argument.")

(defn parse-run-args
  "Return a vector where first elem is the main ns, followed by remaining args"
  [project args]
  (let [proj-ns #(or (:main project) (in/exit-error 1 no-main))
        user-ns #(or (second args)   (in/exit-error 1 no-ns))]
    (cond
      (empty? args)         [(str (proj-ns))]
      (= (first args) "--") [(str (proj-ns)) (rest args)]
      (= (first args) "-m") [(user-ns) (nthrest args 2)]
      :otherwise            [(str (proj-ns)) args])))


(defn task-run
  [project args]
  (let [init-file  (asm-load-init project (in/get-temp-file))
        allp       (all-load-paths project)
        [rns args] (parse-run-args project args)
        _          (in/verbose-init-with init-file)
        r-ex       (concat (clj-main-cmd project) ["-i" init-file "-m" rns] args)]
    ;; run the namespace (r-ex)
    (in/with-process-builder pb (:root project) r-ex
      (in/configure-load-path (.environment ^ProcessBuilder pb) allp)
      (apply in/verbose "Running: " (map pr-str r-ex))
      (in/run-process pb))))


(defn task-test
  [project namespaces]
  (let [init-file (asm-load-init project (in/get-temp-file))
        allp (all-load-paths project)
        nses (mapcat in/scan-namespaces (:test-paths project))
        rtns (if (seq namespaces) namespaces nses)
        nstr (str/join " " (map (partial str "'") rtns))
        _    (in/spit-require-ns init-file rtns)
        _    (in/verbose-init-with init-file)
        expr (format "(use 'clojure.test) (run-tests %s)" nstr)
        exec (concat (clj-main-cmd project) ["-i" init-file "-e" expr])]
    ;; run the tests
    (in/with-process-builder pb (:root project) exec
      (in/configure-load-path (.environment ^ProcessBuilder pb) allp)
      (apply in/verbose "Running: " (map pr-str exec))
      (in/run-process pb))))


(defmacro try-pst
  [& body]
  `(try ~@body
     (catch Throwable t#
       (.printStackTrace t#))))


(defn clr
  "Automate build tasks for ClojureCLR projects

  For more information, run the below command:

  lein clr help"
  [project & [task & args]]
  (case task
    "clean"   (task-clean project)
    "compile" (task-compile project args)
    "help"    (task-help)
    "repl"    (try-pst (task-repl project))
    "run"     (try-pst (task-run  project args))
    "test"    (try-pst (task-test project args))
    "-v"      (binding [in/*verbose* true] (apply clr project args))
    (do (println "No such task: " (pr-str task))
        (task-help))))